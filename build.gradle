plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'distribution'
    id 'checkstyle'
    id 'com.github.spotbugs' version "4.6.0"
    id "com.jfrog.bintray" version "1.8.5"
}

tasks.withType(JavaCompile) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    options.encoding = "UTF-8"
    options.compilerArgs = [ "-Xlint:deprecation,unchecked" ]
}

compileTestJava {
    options.compilerArgs = [ ]
}

task repladceMHH(type: ReplaceMHHCall) {
    classpath = sourceSets.main.output.classesDirs.toList()
    mhhClass = "com.anatawa12.sai.linker.MHH"
}

task repladceMHHS(type: ReplaceMHHSCall) {
    classpath = sourceSets.main.output.classesDirs.toList()
    mhhClass = "com.anatawa12.sai.linker.MHHS"
    methodHandleClass = "com.anatawa12.sai.linker.SMethodHandle"
}

repladceMHH.dependsOn(compileJava)
classes.dependsOn(repladceMHH)

repladceMHHS.dependsOn(compileJava)
classes.dependsOn(repladceMHHS)

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs 'src', 'toolsrc', 'xmlimplsrc'
        }
        resources {
            srcDirs 'src', 'toolsrc'
            exclude "build.xml"
            exclude "manifest"
        }
    }

    test {
        java {
            srcDirs "testsrc", 'examples'
            exclude 'tests/**'
        }
        resources {
            srcDirs "testsrc"
        }
    }

    jmh {
        java {
            srcDirs "benchmarks"
        }
        compileClasspath += sourceSets.test.runtimeClasspath
        runtimeClasspath += sourceSets.test.runtimeClasspath
    }
}

dependencies {
    testImplementation "junit:junit:4.13.1"
    testImplementation "org.yaml:snakeyaml:1.29"
    jmhImplementation project
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.26'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.23'
}

test {
    useJUnit()
    exclude "**/benchmarks/**"

    jvmArgs += '-Xss1280k'

    jacoco.excludes = ['**/testsrc_tests_ecma_3_RegExp_perlstress*']

    systemProperty 'java.awt.headless', 'true'
    systemProperty 'mozilla.js.tests', 'testsrc/tests'
    systemProperty 'mozilla.js.tests.timeout', 60000
    systemProperty 'user.language', 'en'
    systemProperty 'user.country', 'US'
    systemProperty 'user.timezone', 'America/Los_Angeles'
    systemProperty 'dir.encoding', 'UTF-8'
    maxHeapSize = "1g"
    testLogging.showStandardStreams = true
    // Many tests do not clean up contexts properly. This makes the tests much
    // more resilient at the expense of performance.
    forkEvery = 1
    maxParallelForks = 16
}

task sunSpiderBenchmark(type: JavaExec) {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.openjdk.jmh.Main'
    args '-f', '1', '-bm', 'avgt', '-tu', 'us', 'SunSpider'
}

task v8Benchmark(type: JavaExec) {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.openjdk.jmh.Main'
    args '-f', '1', '-bm', 'avgt', '-tu', 'us', 'V8'
}

task testBenchmark() {}
testBenchmark.dependsOn sunSpiderBenchmark
testBenchmark.dependsOn v8Benchmark

task microBenchmarks(type: JavaExec, description: 'JMH benchmarks') {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.openjdk.jmh.Main'
    args '-f', '1', '-bm', 'avgt', '-tu', 'ns', 'ObjectCall'
}

task jmhHelp(type: JavaExec, description: 'JMH benchmarks') {
    classpath = sourceSets.jmh.runtimeClasspath
    main = 'org.openjdk.jmh.Main'
    args '-help'
}

idea {
    module {
        excludeDirs += file('testsrc/tests/src')
        excludeDirs += file('buildGradle')
        excludeDirs += file('build')
        excludeDirs += file('.idea')
        excludeDirs += file('lib')
    }
}

task runtimeJar(type: Jar) {
    dependsOn compileJava
    archiveBaseName = 'sai-runtime'
    from sourceSets.main.output
    excludes = ["com/anatawa12/sai/tools", "com/anatawa12/sai/engine/**", "META-INF/services/**"]
    manifest {
        attributes(
            "Manifest-Version": "1.0",
            "Implementation-Version": project.version,
            "Implementation-Title":  "Sai",
//            "Implementation-Vendor": "Mozilla Foundation",
            "Implementation-URL": "https://github.com/anatawa12/sai",
            "Built-Date": new Date().format("yyyy-MM-dd"),
            "Built-Time": new Date().format("HH:mm:ss"),
            "Bundle-ManifestVersion": "2",
            "Bundle-SymbolicName": "com.anatawa12.sai-runtime",
            "Bundle-Version": project.version.replaceAll("-.*", ""),
            "Export-Package": "com.anatawa12.sai,com.anatawa12.sai.ast,com.anatawa12.sai.annotations"
        )
    }
}

task engineJar(type: Jar) {
    dependsOn compileJava
    archiveBaseName = 'sai-engine'
    from (sourceSets.main.output) {
        include 'com/anatawa12/sai/engine/**'
        include 'META-INF/services/**'
    }
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Implementation-Version": project.version,
                "Implementation-Title":  "Sai ScriptEngine",
//                "Implementation-Vendor": "Mozilla Foundation",
                "Implementation-URL": "https://github.com/anatawa12/sai",
                "Built-Date": new Date().format("yyyy-MM-dd"),
                "Built-Time": new Date().format("HH:mm:ss"),
        )
    }
}

jar {
    dependsOn runtimeJar, engineJar
    from "LICENSE.txt"
    excludes = ["com/anatawa12/sai/engine/**", "META-INF/services/**"]
    manifest {
        attributes(
            "Manifest-Version": "1.0",
            "Main-Class": "com.anatawa12.sai.tools.shell.Main",
            "Implementation-Version": project.version,
            "Implementation-Title":  "Sai",
//            "Implementation-Vendor": "Mozilla Foundation",
            "Implementation-URL": "https://github.com/anatawa12/sai",
            "Built-Date": new Date().format("yyyy-MM-dd"),
            "Built-Time": new Date().format("HH:mm:ss"),
            "Bundle-ManifestVersion": "2",
            "Bundle-SymbolicName": "com.anatawa12.sai",
            "Bundle-Version": project.version.replaceAll("-.*", ""),
            "Export-Package": "com.anatawa12.sai,com.anatawa12.sai.ast,com.anatawa12.sai.annotations"
        )
    }
}

javadoc {
    options.addBooleanOption("-allow-script-in-comments", true)
    options.addStringOption('Xdoclint:html', '-quiet')

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task runtimeJavadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    exclude 'com/anatawa12/sai/tools', 'com/anatawa12/sai/engine'
}

task engineJavadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    include 'com/anatawa12/sai/engine/**'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task runtimeSourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
    exclude 'com/anatawa12/sai/tools', 'com/anatawa12/sai/engine'
}

task engineSourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
    include 'com/anatawa12/sai/engine/**'
}

bintray {
    user = project.hasProperty('BINTRAY_USER') ? project.property('BINTRAY_USER') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('BINTRAY_KEY') ? project.property('BINTRAY_KEY') : System.getenv('BINTRAY_API_KEY')
    publications = ['sai', 'sairuntime', 'saiengine']
    pkg {
        repo = 'maven'
        name = 'com.anatawa12.sai'
        userOrg = user
        licenses = ['MPL-2.0']
        vcsUrl = 'https://github.com/anatawa12/sai.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = 'v' + project.version
        }
    }
}

static def configurePom(root) {
    root.appendNode('description', """
    Sai is an fork of Rino, an open-source implementation of JavaScript in Java.
    This is for fixRTM project.
""")
    root.appendNode("url", "https://github.com/anatawa12/sai")

//    def p = root.appendNode("parent")
//    p.appendNode("groupId", "org.sonatype.oss")
//    p.appendNode("artifactId", "oss-parent")
//    p.appendNode("version", "7")

    def l = root.appendNode("licenses").appendNode("license")
    l.appendNode("name", "Mozilla Public License, Version 2.0")
    l.appendNode("url", "http://www.mozilla.org/MPL/2.0/index.txt")

    def scm = root.appendNode("scm")
    scm.appendNode("connection", "scm:git:git@github.com:anatawa12/sai.git")
    scm.appendNode("developerConnection", "scm:git:git@github.com:anatawa12/sai.git")
    scm.appendNode("url", "git@github.com:anatawa12/sai.git")

//    def o = root.appendNode("organization")
//    o.appendNode("name", "The Mozilla Foundation")
//    o.appendNode("url", "http://www.mozilla.org")
}

publishing {
    publications {
        sai(MavenPublication) {
            groupId 'com.anatawa12.sai'
            artifactId 'sai'

            pom.withXml {
                def root = asNode()

                configurePom(root)
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }

        sairuntime(MavenPublication) {
            groupId 'com.anatawa12.sai'
            artifactId 'sai-runtime'
            artifacts = [runtimeJar, runtimeSourceJar, runtimeJavadocJar]

            pom.withXml {
                def root = asNode()
                configurePom(root)
            }
        }

        saiengine(MavenPublication) {
            groupId 'com.anatawa12.sai'
            artifactId 'sai-engine'
            artifacts = [engineJar, engineSourceJar, engineJavadocJar]

            pom.withXml {
                def root = asNode()
                configurePom(root)

                def deps = root.appendNode("dependencies")
                def sai = deps.appendNode("dependency")
                sai.appendNode("groupId", "com.anatawa12.sai")
                sai.appendNode("artifactId", "sai")
                sai.appendNode("version", project.version)
            }
        }
    }
}

spotbugs {
    effort = "less"
    reportLevel = "medium"
    excludeFilter = file("./spotbugs-exclude.xml")
}

jacocoTestReport.dependsOn test
jacocoTestReport {
    reports {
        html.destination file("${buildDir}/jacocoHtml")
    }
}

checkstyle {
    configFile = file("${projectDir}/checkstyle.xml")
    sourceSets = [ project.sourceSets.main ]
}

distributions {
    main {
        contents {
            from(sourceSets.main.java) {
                exclude 'man'
                into 'sai' + project.version + '/src'
            }
            from(sourceSets.main.resources) {
                exclude '**/*.java'
                into 'sai' + project.version + '/src'
            }
            from(javadoc.destinationDir) {
                into 'sai' + project.version + '/docs'
            }
            from(jar.outputs.files) {
                into 'sai' + project.version + '/lib'
            }
            from(sourceSets.main.allSource) {
                include 'man/*.1'
                into 'sai' + project.version
            }
            from(file(".")) {
                include '*.txt', '*.md', 'build.gradle', 'build.properties', 'gradle.properties',
                        'gradle/**', 'gradlew'
                into 'sai' + project.version
            }
            into "/"
        }
    }
}

distZip.dependsOn javadoc, jar, sourceJar, runtimeSourceJar
